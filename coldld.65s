	;OUP/M COLDLD WRITTEN BY SHAO,JIAN-XIONG 
		;ON JULY 1982.
		*=$2200
		JMP		CDBTLD
BIOSA1	.WORD	$2280 
BIOSA2	.WORD	$DE00 
BIOSPG	.BYTE	$0C
MVREG1=$53
MVREG2=$55
IOBFFG=$39 
TKNDER=$44 
TRKCRN=$48 
DVACIA=$C010 
FRYCST=$E41B 
WBOOT=$E477 
BOOT=$E42F
SFTSWT=$F701
FRYCCT	.BYTE $31,$31,$31,$31,$31
	.BYTE $62,$62,$62,$62 
	.BYTE $A0,$A0,$A0,$A0
	.BYTE $A0,$A0,$A0,$A0
		;
CDBTLD=*			;COLD BOOT LOADER
		;
		CLD						;MOVE BIOS AND PART OF
		LDA 	BIOSA1 			;BDOS TO ITS REGULAR POSITION
		STA 	MVREG1  
		LDA 	BIOSA1+1  
		STA 	MVREG1+1  
		LDA 	BIOSA2  
		STA 	MVREG2  
		LDA 	BIOSA2+1
		STA 	MVREG2+1 
		LDX	BIOSPG 
		LDY 	#$00
C_BTLD=* 
		LDA	(MVREG1),Y 
		STA	(MVREG2),Y
		INY
		BNE		C_BTLD  	
		INC		MVREG1+1 	
		INC		MVREG2+1 	
		DEX		
		BNE		C_BTLD 	
		LDA		#$34			;ADJUST FREQUENCY
		STA		SFTSWT 				
		LDY		#$00	
		LDA		#$03	
		STA		DVACIA 	
		LDA		#$38
		STA		DVACIA 
		STA		DVACIA+1
		PHA	
		PLA	
		PHA	
		PLA	
		PHA	
		PLA	
		STX		DVACIA+1
AD_TFY=* 
		LDA		DVACIA
		BMI		A_JTFY
		INY	
		BNE		AD_TFY
A_JTFY=* 
		LDA		FRYCCT,Y 
		STA		FRYCST
		JSR		BOOT			;INITIALIZE ALL INTERFACE
		LDA		#$00 
		STA		IOBFFG
		STA		TKNDER			;MARK HEAD POSITION TO 0 
		STA		TRKCRN
		LDA		#$FF			;SET COLD BOOT FLAG
		TAX 
		TXS 
		PHA
		JMP		WBOOT			;WBOOT CCP AND REST OF BDOS
 
